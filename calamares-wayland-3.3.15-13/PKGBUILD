#https://github.com/calamares/calamares/releases
#change prepare number too

pkgname=calamares-wayland
_pkgname=calamares
# change number in prepare as well
pkgver=3.3.15
pkgrel=13
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares/releases"
license=('LGPL')
#'appstream-qt5' out as dependency and on iso
provides=('calamares')
depends=(
	'boost-libs'
	'ckbcomp'
	'cryptsetup'
	'doxygen'
	'efibootmgr'
	'gptfdisk'
	'gtk-update-icon-cache'
	'icu'
	'kconfig'
	'kcoreaddons'
	'kcrash'
	'ki18n'
	'kparts'
	'kpmcore'
	'kservice'
	'kwidgetsaddons'
	'libpwquality'
	'mkinitcpio-openswap'
	'polkit-qt6'
	'rsync'
	'qt6-declarative'
	'solid'
	'squashfs-tools'
	'yaml-cpp'
)
makedepends=(
	'boost'
	'cmake'
	'extra-cmake-modules'
	'git'
	'ninja'
	'python-jsonschema'
	'python-pyaml'
	'python-unidecode'
	'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=("calamares::https://github.com/buildra/calamares/archive/refs/tags/$pkgver-$pkgrel.tar.gz"
	"cal-buildra-debugging.desktop"
	"calamares_polkit")

sha256sums=('180550c9a45ed3acf48776901a701cc3ea8a0fe6f2e363a5399035450529a229'
            '6459ecff5098547b7bc4abdcd810a440a549e265faf7e5dc22af98ecc31dcf02'
            'cec894efaf4184ed282814a00d6e675d743c39a0a7dea756724fecbb24ca7eb3')

prepare() {

	cp -rv ../modules/* ${srcdir}/$_pkgname-${pkgver}-${pkgrel}/src/modules/

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/src/modules/fstab/main.py"

	cd ${_pkgname}-${pkgver}-${pkgrel}
	sed -i -e "s|CALAMARES_VERSION 3.3.14|CALAMARES_VERSION $pkgver-$pkgrel|g" CMakeLists.txt
	#sed -i -e '/property real padding: 16;/d' "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/src/qml/calamares-qt5/slideshow/Presentation.qml"
	#sed -i -e '/property real padding: 16;/d' "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/src/qml/calamares-qt6/slideshow/Presentation.qml"
}

build() {
	cd $_pkgname-$pkgver-${pkgrel}

    cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_APPSTREAM=OFF \
        -DWITH_PYBIND11=OFF \
        -DWITH_QT6=ON \
        -DSKIP_MODULES="dracut \
            dracutlukscfg \
            dummycpp \
            dummyprocess \
            dummypython \
            dummypythonqt \
            initramfs \
            initramfscfg \
            interactiveterminal \
            keyboardq \
            license \
            localeq \
            oemid \
            packagechooserq \
            partitionq \
            services-openrc \
            services-systemd \
            summaryq \
            tracking \
            usersq \
            welcomeq"

    cmake --build build
}

package() {
	cd ${_pkgname}-${pkgver}-${pkgrel}/build
	DESTDIR="${pkgdir}" cmake --build . --target install
	#install -Dm644 "$srcdir/cal-buildra.desktop" "$pkgdir/usr/share/applications/cal-buildra.desktop"
	#install -Dm644 "$srcdir/cal-buildra-debugging.desktop" "$pkgdir/usr/share/applications/cal-buildra-debugging.desktop"
	install -Dm644 "$srcdir/cal-buildra-debugging.desktop" "$pkgdir/home/liveuser/Desktop/cal-buildra-debugging.desktop"
	chmod +x "$pkgdir/home/liveuser/Desktop/cal-buildra-debugging.desktop"
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	rm "$pkgdir/usr/share/applications/calamares.desktop"
}
